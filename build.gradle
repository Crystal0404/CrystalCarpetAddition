plugins {
    id("maven-publish")
    id("fabric-loom").version("1.4-SNAPSHOT").apply(false)
    id("org.ajoberstar.grgit").version("5.2.0")
    id("com.replaymod.preprocess").version("SNAPSHOT")
}

preprocess {
    def mc1194 = createNode("1.19.4", 1_19_04, "yarn")
    def mc1201 = createNode("1.20.1", 1_20_01, "yarn")
    def mc1202 = createNode("1.20.2", 1_20_02, "yarn")
    def mc1204 = createNode("1.20.4", 1_20_04, "yarn")

    mc1194.link(mc1201, null)
    mc1201.link(mc1202, null)
    mc1202.link(mc1204, null)
}

ext {
    env = System.getenv()

    getVersionGit = { List paths ->
        if (grgit == null) {
            return "nogit"
        }

        List latestCommits = paths.isEmpty() ? grgit.log(maxCommits: 1) : grgit.log(paths: paths, maxCommits: 1)
        return latestCommits.isEmpty() ? "uncommited" : "${latestCommits.get(0).id.substring(0, 7)}"
    }

    getBuildNumber = {
        return ext.env.GITHUB_RUN_NUMBER ? ext.env.GITHUB_RUN_NUMBER : Integer.MAX_VALUE
    }

    getVersionType = {
        switch (ext.env.BUILD_TYPE) {
            case "RELEASE":
                return "stable"
            case "BETA":
                return "beta"
            default:
                return "dev"
        }
    }

    getVersionPatch = { List paths ->
        if (grgit == null) {
            return 0
        }

        List latestCommits = paths.isEmpty() ? grgit.log() : grgit.log(paths: paths)
        return latestCommits.size()
    }

    getMavenArtifactVersion = {
        return ext.getVersionType() == "stable" ? "${project.mod_version}.${ext.getVersionPatch([])}" : project.version
    }
}

setVersion("${project.mod_version}")

tasks.register("cleanPreprocessSources") {
    it.group("${project.mod_id}")

    doFirst {
        subprojects {
            def path = project.projectDir.toPath().resolve("build/preprocessed")
            path.toFile().deleteDir()
        }
    }
}
